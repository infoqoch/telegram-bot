plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}


sourceCompatibility = '17'
compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // di container
    implementation 'org.springframework:spring-context:6.2.15'

    implementation 'ch.qos.logback:logback-classic:1.5.23'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    testCompileOnly 'org.projectlombok:lombok:1.18.42'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.42'

    // 테스트
    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'
    testImplementation 'org.mockito:mockito-core:5.21.0'
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'
    testImplementation 'org.assertj:assertj-core:3.27.6'
}

build{

}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications{

        mavenJava(MavenPublication){

            groupId = 'io.github.infoqoch'
            artifactId = 'telegram-bot'
            version = '0.3.0'
            from components.java

            pom {
                name = 'telegram-bot'
                description = 'simply telegram bot API written in java'
                url = 'https://github.com/infoqoch/telegram-bot'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'infoqoch'
                        name = 'infoqoch'
                        email = 'infoqoch@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/infoqoch/telegram-bot.git'
                    developerConnection = 'scm:git:ssh://github.com/infoqoch/telegram-bot.git'
                    url = 'https://github.com/infoqoch/telegram-bot'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    required { gradle.taskGraph.hasTask("publish") && !gradle.taskGraph.hasTask("publishToMavenLocal") }
    sign publishing.publications.mavenJava
}

javadoc {
    options.encoding = 'UTF-8'
}
