plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}


sourceCompatibility = '8'
compileJava.options.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // di container
    implementation 'org.springframework:spring-context:5.3.21'

    implementation 'ch.qos.logback:logback-classic:1.4.4'
    implementation 'org.apache.httpcomponents:httpclient:4.5.9'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    //테스트에서 lombok 사용
    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    // 테스트
    implementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    implementation 'org.mockito:mockito-core:4.5.1'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.assertj:assertj-core:3.22.0'
}

build{

}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications{

        mavenJava(MavenPublication){

            groupId = 'io.github.infoqoch'
            artifactId = 'telegram-bot'
            version = '0.2.4'
            from components.java

            pom {
                name = 'telegram-bot'
                description = 'simply telegram bot API written in java'
                url = 'https://github.com/infoqoch/telegram-bot'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'infoqoch'
                        name = 'infoqoch'
                        email = 'infoqoch@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/infoqoch/telegram-bot.git'
                    developerConnection = 'scm:git:ssh://github.com/infoqoch/telegram-bot.git'
                    url = 'https://github.com/infoqoch/telegram-bot'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.properties["ossrhUsername"]
                password = project.properties["ossrhPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    options.encoding = 'UTF-8'
}
